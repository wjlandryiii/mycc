# This grammar is based off of the PERL regex grammar.
# This grammar is (TODO: verify it is) LL(1).

<re>
	: <simple_re> <union_>
	;

<union_>
	: '|' <simple_re> <union_>
	| {}
	;

<simple_re>
	: <basic_re> <concatenation>
	;

<concatenation>
	: <basic_re> <concatenation>
	| {}
	;

<basic_re>
	: <elementary_re> <basic_re_prime>
	;

<basic_re_prime>
	: '*' <basic_re_prime>
	| '+' <basic_re_prime>
	| '?' <basic_re_prime>
	| {}
	;

<elementary_re>
	: <group>
	| <any>
	| <eos>
	| <character>
	| <set>
	;

<group>
	: '(' <re> ')'
	;

<any>
	: '.'
	;

<eos>
	: '$'
	;

<character>
	: <nonmetachar>
	| '\' <metachar>
	;

<nonmetachar>
	: 'a'
	| 'b'
	| 'c'
	| 'x'
	| 'y'
	| 'z'
	| 'A'
	| 'B'
	| 'C'
	| 'X'
	| 'Y'
	| 'Z'
	| '0'
	| '1'
	| '2'
	| '3'
	| '4'
	| '5'
	| '6'
	| '7'
	| '8'
	| '9'
	;

<metachar>
	: '\\'
	| '|'
	| '*'
	| '+'
	| '.'
	| '['
	| ']'
	| '$'
	| '('
	| ')'
	| '.'
	| '-'
	| '^'
	;

<set>
	: <positive-set>
	| <negative-set>
	;

<positive-set>
	: "[" <set-items> "]"
	;

<negative-set>
	: "[" "^" <set-items> "]"
	;

<set-items>
	: <range> <set-items>
	| <character> <set-items>
	| {}
	;
	
<range>
	: <character> "-" <character>
	;
